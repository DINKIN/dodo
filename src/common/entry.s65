                .include "zeropage.inc65"
                .include "display.inc65"
                .include "util.inc65"
                .include "io.inc65"
                .include "text.inc65"

                .setcpu "6502"

                .export _init
                .export _exit


                .segment "VECTORS"
                .addr nmi
                .addr _init
                .addr irq

                .code

nmi:            rti

irq:                    
                pha
                txa
                pha
                tya
                pha

                clc
                cld
                clv
                jsr irq_reset       ; Mark interrupt as read
                
                pla
                tay
                pla
                tax
                pla

                rti

_init:
                sei                
                cld
                clv
                ldx #$ff
                txs

                jsr ssd1305_init
                jsr io_init
                jsr text_init

.ifdef __DEVICE__
@wait_key:
                lda $6001
                ldx invert_buttons
                bne @not_inverted
                eor #$ff
@not_inverted:                
                pha
                and #16
                beq @load
                pla
                and #32
                beq @flash
                jmp @wait_key

@flash:
                jsr led_off
                jsr serial_init
                jsr serial_copy
                jsr led_on
                jsr flash_game
                jsr led_off

@load:
.endif
                jsr load_game

                jmp $1d00

_exit:          
end:            jmp end
